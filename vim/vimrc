" ##############################################################################
" # GENERAL SETTINGS                                                           #
" ##############################################################################

" Use the Pathogen plugin to install plugins and
" runtime files in their own private directories
runtime plugins/pathogen/autoload/pathogen.vim

call pathogen#infect("plugins")
call pathogen#infect()
call pathogen#helptags()

syntax on                         " Enable syntax highlighting

if has("autocmd")
    filetype plugin indent on
    "           │     │    └─────── Enable file type detection
    "           │     └──────────── Enable loading of indent file
    "           └────────────────── Enable loading of plugin files
endif

set t_Co=256                      " Enable full-color support
colorscheme Tomorrow-Night             " Use custom color scheme
set autoindent                    " Copy indent to the new line
set background=dark               " Use colors that look good on a dark bg

set backspace=indent              " ┐ set backspace+=eol                " │ Allow `backspace` in insert mode
set backspace+=start              " ┘

set backupdir=~/.vim/backups      " Set directory for backup files

set clipboard=unnamed             " ┐
                                  " │ Use the system clipboard
if has("unnamedplus")             " │ as the default register
    set clipboard+=unnamedplus    " │
endif                             " ┘

set cpoptions+=$                  " When making a change, don't redisplay the
                                  " line, and instead, put a `$` sign at the
                                  " end of the changed text

set colorcolumn=81                " Highlight certain columns

set directory=~/.vim/swaps        " Set directory for swap files
set encoding=utf-8 nobomb         " Use UTF-8 without BOM
set history=5000                  " Increase command line history
set hlsearch                      " Enable search highlighting
set ignorecase                    " Ignore case in search patterns
set incsearch                     " Highlight search pattern as it's being typed
set laststatus=2                  " Always show the status line

set listchars=tab:▸\              " ┐
set listchars+=trail:·            " │ Use custom symbols to
set listchars+=eol:↴              " │ represent invisible characters
set listchars+=nbsp:_             " ┘

set magic                         " Enable extended regexp
set mousehide                     " Hide mouse pointer while typing
set nocompatible                  " Don't make vim, vi-compatibile
set noerrorbells                  " Disable error bells

set nojoinspaces                  " When using the join command, only insert a
                                  " single space after a `.`, `?` or `!`

set nostartofline                 " Kept the cursor on the same column
set number                        " Show line number

set numberwidth=4                 " Increase the minimal number of columns used
                                  " for the `line number`

set report=0                      " Report the number of lines changed
set ruler                         " Show cursor position

set scrolloff=5                   " When scrolling, keep the cursor 5 lines
                                  " below the top and 5 lines above the bottom
                                  " of the screen

set shortmess=aAItW               " Avoid all the hit-enter prompts
set showcmd                       " Show the (partial) command being typed
set showmode                      " Show current mode
set spelllang=en_us               " Use `en_us` as the spellchecking language

set smartcase                     " Override `ignorecase` option if the search
                                  " pattern contains uppercase characters

set synmaxcol=2500                " Limit syntax highlighting (this avoids the
                                  " very slow redrawing when files contain long
                                  " lines)

set tabstop=4                     " ┐
set softtabstop=4                 " │ Set global <TAB> settings
set shiftwidth=4                  " │ http://vimcasts.org/e/2
set expandtab                     " ┘

set ttyfast                       " Enable fast terminal connection
set undodir=~/.vim/undos          " Set directory for undo files
set undofile                      " Automatically save undo history
set virtualedit=all               " Allow cursor to be positioned anywhere

set visualbell                    " ┐
set noerrorbells                  " │ Disable beeping and window flashing
set t_vb=                         " ┘ http://vim.wikia.com/wiki/Disable_beeping

set wildmenu                      " Enable enhanced command-line completion (by
                                  " hitting <TAB> in command mode, vim will show
                                  " the possible matches just above the command
                                  " line with the first match highlighted)

set winminheight=0                " Allow windows to be squashed to 0 lines



" ------------------------------------------------------------------------------
" | Automatic Commands                                                         |
" ------------------------------------------------------------------------------

if has("autocmd")

    " Automatically switch to absolute line numbers when vim loses focus
    autocmd FocusLost * :set number
    " Automatically switch to relative line numbers when vim gains focus
    autocmd FocusGained * :set relativenumber
    " Automatically switch to absolute line numbers when vim is in insert mode
    autocmd InsertEnter * :set number
    " Automatically switch to relative line numbers when vim is in normal mode
    autocmd InsertLeave * :set relativenumber

    " Custom syntax for different languages
    autocmd FileType javascript setlocal ts=2 sts=2 sw=2 expandtab
    autocmd FileType jade setlocal ts=2 sts=2 sw=2 expandtab

    " Treat `.json` files as JavaScript
    autocmd BufNewFile, BufRead *.json setfiletype json syntax=javascript

    " Strip trailing whitespace automatically when the file is saved
    autocmd BufWritePre * :call StripWhitespaces()

endif

" ------------------------------------------------------------------------------
" | Highlighting                                                               |
" ------------------------------------------------------------------------------

" Terminal types:
"
"   1) term  (normal terminals, e.g.: vt100, xterm)
"   2) cterm (color terminals, e.g.: MS-DOS console, color-xterm)
"   3) gui   (GUIs)

" highlight ColorColumn                              ctermbg=Cyan

set statusline=%1*\ [%n]\ [%f]%m%r%h%w%y[%{&ff}:%{strlen(&fenc)?&fenc:'none'}]%=%(\ %c,%l/%L\ %)%P
"               │     │     │  │ │ │ │ │    │                │                 │     │ │   │    └─ percent through file
"               │     │     │  │ │ │ │ │    │                │                 │     │ │   └─ total number of lines
"               │     │     │  │ │ │ │ │    │                │                 │     │ └─ current line number
"               │     │     │  │ │ │ │ │    │                │                 │     └─ current column number
"               │     │     │  │ │ │ │ │    │                │                 └─ left/right separator
"               │     │     │  │ │ │ │ │    │                └─ file encoding
"               │     │     │  │ │ │ │ │    └─ file format
"               │     │     │  │ │ │ │ └─ file type
"               │     │     │  │ │ │ └─ preview window flag
"               │     │     │  │ │ └─ help file flag
"               │     │     │  │ └─ readonly flag
"               │     │     │  └─ modified flag
"               │     │     └─ path to the file
"               │     └─ buffer number
"               └─ User1 highlight

" ------------------------------------------------------------------------------
" | Key Mappings                                                               |
" ------------------------------------------------------------------------------

" Use a different mapleader (default is "\")
let mapleader = ","

" Redefine trigger key for Emmet
" http://docs.emmet.io/cheat-sheet/
let g:user_emmet_leader_key='<C-E>'

" [,* ] Search and replace the word under the cursor
nnoremap <leader>* :%s/\<<C-r><C-w>\>//<Left>

" [,cc] Toggle code comments
" https://github.com/tomtom/tcomment_vim
noremap <leader>cc :TComment<CR>

" [,cr] Calculate result
" http://vimcasts.org/e/56
nnoremap <leader>cr 0yt=A<C-r>=<C-r>"<CR><Esc>

" [,cs] Clear search
map <leader>cs <Esc>:noh<CR>

" [,h ] JSHint the code
" https://github.com/Shutnik/jshint2.vim
nnoremap <leader>h :JSHint<CR>

" [,l ] Toggle `set list`
nmap <leader>l :set list!<CR>

" [,n ] Toggle `set relativenumber`
" http://jeffkreeftmeijer.com/2012/relative-line-numbers-in-vim-for-super-fast-movement/
function! ToggleRelativeLineNumbers()

    if ( &relativenumber == 1 )
        set number
    else
        set relativenumber
    endif

endfunction

nnoremap <leader>n :call ToggleRelativeLineNumbers()<CR>

" [,sl] Toggle show limits
function! ToggleLimits()

    " [51,73]
    "   - git commit message
    "     http://tbaggery.com/2008/04/19/a-note-about-git-commit-messages.html
    " [81]
    "   - general use

    if ( &colorcolumn == "81" )
        set colorcolumn+=51,73
    else
        set colorcolumn-=51,73
    endif

endfunction

nnoremap <leader>sl :call ToggleLimits()<CR>

" [,ss] Strip whitespace
function! StripWhitespaces()

    " save last search and cursor position
    let searchHistory = @/
    let cursorLine = line(".")
    let cursorColumn = col(".")

    " strip trailing whitespaces
    %s/\s\+$//e

    " merge consecutive empty lines
    "g/^\s*$/,/\S/-j

    " restore previous search history and cursor position
    let @/ = searchHistory
    call cursor(cursorLine, cursorColumn)

endfunction

nnoremap <leader>ss :call StripWhitespaces()<CR>

" [,v ] Make the opening of the `.vimrc` file easier
nmap <leader>v :vsp $MYVIMRC<CR>

" [,W ] Sudo write
noremap <leader>W :w !sudo tee %<CR>


" ##############################################################################
" # PLUGIN RELATED SETTINGS                                                    #
" ##############################################################################

" ------------------------------------------------------------------------------
" | Emmet                                                                      |
" ------------------------------------------------------------------------------

" Custom Emmet snippets
" http://docs.emmet.io/customization/snippets/
let NERDTreeShowHidden=1
map <silent><C-n> :NERDTreeToggle<ENTER>

let g:EasyMotion_do_mapping = 0
nmap s <Plug>(easymotion-s)
vmap s <Plug>(easymotion-s)
map <leader>j <Plug>(easymotion-j)
map <leader>k <Plug>(easymotion-k)
map / <Plug>(easymotion-sn)
omap / <Plug>(easymotion-tn)
let g:EasyMotion_smartcase = 1
let g:EasyMotion_use_smartsign_us = 1
let g:EasyMotion_add_search_history = 0
hi EasyMotionTarget2First ctermfg=134
hi EasyMotionTarget2Second ctermfg=150

" buffer
map <F2> :MBEbn<cr>
map <F3> :MBEbp<cr>

" session
let g:startify_session_persistence = 1
let g:startify_session_delete_buffers = 1
let g:startify_restore_positioon = 1
let g:startify_custom_header = [
                \ '   __      ___            ______ ____   ',
                \ '   \ \    / (_)           |____  |___ \ ',
                \ '    \ \  / / _ _ __ ___       / /  __) |',
                \ '     \ \/ / | | ''_ ` _ \     / /  |__ <',
                \ '      \  /  | | | | | | |   / /   ___) |',
                \ '       \/   |_|_| |_| |_|  /_(_) |____/ ',
                \ '',
                \ '',
                \ ]
hi StartifyBracket ctermfg=240
hi StartifyNumber  ctermfg=215
hi StartifyPath    ctermfg=245
hi StartifySlash   ctermfg=240
hi StartifySpecial ctermfg=240
hi StartifyHeader  ctermfg=114
hi StartifyFooter  ctermfg=240
"hi StartifyFile    ctermfg=111

"airline
let g:airline_powerline_fonts = 1
let g:airline_theme = 'bubblegum'

"tmux
if exists('$TMUX')
  function! TmuxOrSplitSwitch(wincmd, tmuxdir)
    let previous_winnr = winnr()
    silent! execute "wincmd " . a:wincmd
    if previous_winnr == winnr()
      call system("tmux select-pane -" . a:tmuxdir)
      redraw!
    endif
  endfunction

  let previous_title = substitute(system("tmux display-message -p '#{pane_title}'"), '\n', '', '')
  let &t_ti = "\<Esc>]2;vim\<Esc>\\" . &t_ti
  let &t_te = "\<Esc>]2;". previous_title . "\<Esc>\\" . &t_te

  nnoremap <silent> <C-h> :call TmuxOrSplitSwitch('h', 'L')<cr>
  nnoremap <silent> <C-j> :call TmuxOrSplitSwitch('j', 'D')<cr>
  nnoremap <silent> <C-k> :call TmuxOrSplitSwitch('k', 'U')<cr>
  nnoremap <silent> <C-l> :call TmuxOrSplitSwitch('l', 'R')<cr>
else
  map <C-h> <C-w>h
  map <C-j> <C-w>j
  map <C-k> <C-w>k
  map <C-l> <C-w>l
endif

"indent lines
let g:indentLine_noConcealCursor=1

" ##############################################################################
" # LOCAL SETTINGS                                                             #
" ##############################################################################

" Load local settings if they exist
if filereadable(glob("~/.vimrc.local"))
    source ~/.vimrc.local
endif

nnoremap <up> <nop>
nnoremap <down> <nop>
nnoremap <left> <nop>
nnoremap <right> <nop>
inoremap <esc> <nop>

nnoremap j gj
nnoremap k gk
nnoremap <C-j> <C-W>j
nnoremap <C-k> <C-W>k
nnoremap <C-h> <C-W>h
nnoremap <C-l> <C-W>l
inoremap jk <esc>
inoremap [3~ <Del>


if has("autocmd")
  au InsertEnter * silent execute "!gconftool-2 --type string --set /apps/gnome-terminal/profiles/Default/cursor_shape ibeam"
  au InsertLeave * silent execute "!gconftool-2 --type string --set /apps/gnome-terminal/profiles/Default/cursor_shape block"
  au VimLeave * silent execute "!gconftool-2 --type string --set /apps/gnome-terminal/profiles/Default/cursor_shape block"
endif

set relativenumber
